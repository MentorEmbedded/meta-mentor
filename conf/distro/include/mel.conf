DISTRO_VERSION = "2013.03"
MAINTAINER = "Mentor Graphics Corporation <embedded_support@mentor.com>"
TARGET_VENDOR = "-mel"
SDK_VENDOR = "-melsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

# Target i686 for the sdk binaries, rather than the build arch.
#
# Other options: i586, x86_64
SDKMACHINE ?= "i686"

# Application Development Environment
ADE_PROVIDER = "Mentor Graphics Corporation"
ADE_SITENAME = "Application Development Environment for ${DISTRO_NAME}"
ADE_SECTIONS = "devel bootloaders kernel source sdk"
ADE_SECTIONS_EXCLUDED = "locale"

require conf/distro/include/mel-versions.conf
require conf/distro/include/mel-providers.conf
require conf/distro/include/mel-vardeps.conf

# Ensure we can fetch from private github repositories with https
FETCHCMD_wget += "--auth-no-challenge"

INHERIT_DISTRO ?= "debian devshell sstate license deploy-license-manifest"

# Ensure that all listed licenses show up in the manifest, even if the license
# files weren't populated for that recipe/package.
INHERIT_DISTRO += "license-fixup"

# Configure non-linux-yocto kernels for lttng
INHERIT += "kernel-lttng"

# Support USER_FEATURES
INHERIT += "user_features"

# Populate an additional isolated sstate directory just for the sstates used
# in this build. This is useful for our release process.
INHERIT += "isolated-sstate-dir"

# Restore any available saved headrevs
DUMP_HEADREVS_DB ?= '${MELDIR}/${MACHINE}/saved_persist_data.db'
INHERIT += "restore-dumped-headrevs"

# Ensure we get both 32 and 64 bit pseudo on 64 bit hosts
NO32LIBS ?= "0"

# Make our libdir obey BASE_LIB from the tuning files by default
MULTILIBS ?= ""
require conf/multilib.conf

# Mask out the TI angstrom/systemd requirements
BBMASK ?= "/meta-ti/recipes-misc/|/meta-systemd/meta-efl/|/meta-systemd/meta-multimedia/"

# Ensure that we implement shared state reuse handling for non-target recipes
require conf/distro/include/sstate.inc

# Ensure the emitted locale packages are in that section, so they can be
# easily excluded from the ADE (or categorized correctly, when included)
LOCALE_SECTION = "locale"

# A problem we have is that while the info page for ld says that sysv is the
# default in RHEL5, it is actually not and it defaults to gnu hash.  This
# in turn results in binaries that won't run on older systems.  We change to
# forcing both hash types to be included to fix this issue.
BUILD_LDFLAGS += "-Wl,--hash-style=both"

# we need the u-boot image types available
IMAGE_CLASSES ?= "image_types_uboot"

# Default to these image types
IMAGE_FSTYPES ?= "tar.bz2 ext3"
UBI_VOLNAME = "rootfs"

# Quadruple the normal. 'du' is not a good way to really see how much
# space will be needed and fails badly as the fs size grows.
IMAGE_ROOTFS_EXTRA_SPACE = "40960"

# Add nls if we're supporting.
DISTRO_FEATURES_append = " ${@['','nls'][bb.data.getVar('USE_NLS', d, 1) == 'yes']}"

# Sane default locales for images
GLIBC_GENERATE_LOCALES ?= "en_US en_US.UTF-8"
IMAGE_LINGUAS ?= "en-us"

# Turn off debug information on host-run utils
BUILD_OPTIMIZATION += "-g0"

# Targets for qemu
QEMU_TARGETS += "mips64 mips64el sh4"

# We prefer busybox rather than tinylogin
VIRTUAL-RUNTIME_login_manager = "busybox"

# We want information about image contents
INHERIT += "buildhistory"
BUILDHISTORY_DIR ?= "${TOPDIR}/buildhistory"
BUILDHISTORY_COMMIT ?= "1"

# We want information about task executions
INHERIT += "buildstats"
INHERIT += "buildstats-summary"

# Ensure our external toolchain is sane
require conf/distro/include/toolchain-sanity.inc

# Add symlink to ${KERNEL_IMGTYPE}
require conf/distro/include/kernel-link.inc

# Default to ipk packaging
PACKAGE_CLASSES ?= "package_ipk"

# Force early expansion of recursing package vars
PACKAGE_CLASSES += " package_early_expand"

# More sane usability for the archiver classes
ARCHIVER_MODE ?= "none"
ARCHIVER_MODE[type] = "choice"
ARCHIVER_MODE[choices] = "none original patched configured"
ARCHIVER_CLASS = "${@'archive-${ARCHIVER_MODE}-source' \
                     if ARCHIVER_MODE != 'none' else ''}"
INHERIT += "${ARCHIVER_CLASS}"

# Ensure we have license-filtered sources available
COPYLEFT_LICENSE_INCLUDE = '*'
COPYLEFT_LICENSE_EXCLUDE = 'CLOSED Proprietary* Mentor Freescale EULA INTEL NetLogic'
COPYLEFT_RECIPE_TYPES ?= '${COPYLEFT_AVAILABLE_RECIPE_TYPES}'
INHERIT += "copyleft_compliance"

# Break up copyleft for use by archive-release
INHERIT += "extra_layerinfo"
COPYLEFT_SOURCES_BASE_DIR ?= '${DEPLOY_DIR}/release-downloads'
RECIPE_LAYER_BASENAME = "${@os.path.basename(get_layer_rootdir(RECIPE_LAYERPATH, d))}"
COPYLEFT_SOURCES_DIR = '${COPYLEFT_SOURCES_BASE_DIR}/${RECIPE_LAYER_BASENAME}'

# Ensure we have what we need for the below type checks
OE_IMPORTS += "oe.terminal"

# Do an up front type check to sanity check user configuration
INHERIT += "typecheck"

# Default to no automatic spawned terminals -- expicit is better than implicit
PATCHRESOLVE = "noop"

# Defaults for meta-ti machines missing it, as some recipes require it
MACHINE_KERNEL_PR_beagleboard ?= "r1"

# Additional distros we test on
SANITY_TESTED_DISTROS += "\
    Ubuntu 10.04.1 LTS \n \
    CentOS release 5.4 (Final) \n \
    CentOS release 5.5 (Final) \n \
    CentOS release 5.6 (Final) \n \
    CentOS release 5.7 (Final) \n \
    CentOS release 5.8 (Final) \n \
    CentOS release 5.9 (Final) \n \
"

# We aren't quite so picky as poky
WARN_QA = "ldflags useless-rpaths rpaths"
ERROR_QA = "dev-so debug-deps dev-deps debug-files arch la2 pkgconfig la perms"

# Disable reliance upon upstream URIs, as we want our customers to be able to
# build without network connectivity
CONNECTIVITY_CHECK_URIS = ""

# Work around do_populate_lic(sstate_create_package) / do_rootfs race
# Without this, bitbake defaults the dirs to ${B}, which do_rootfs removes due
# to its cleandirs for recipes where ${B} == ${S}.
sstate_create_package[dirs] = "${TOPDIR}"

# Default to the Xorg X server
XSERVER ?= "\
    xserver-xorg \
    xf86-input-evdev \
    xf86-input-mouse \
    xf86-input-keyboard \
    xf86-video-fbdev \
"

# Package groups
VIRTUAL-RUNTIME_lttng         ?= '${@base_contains("MACHINE_FEATURES", "lttng", "packagegroup-tools-lttng", base_contains("MACHINE_FEATURES", "lttng2", "packagegroup-tools-lttng2", "", d), d)}'
VIRTUAL-RUNTIME_lttng_p2020rdb ?= "packagegroup-tools-lttng2"
VIRTUAL-RUNTIME_lttng_beaglebone ?= "packagegroup-tools-lttng2"
VIRTUAL-RUNTIME_lttng_p4080ds ?= "packagegroup-tools-lttng2"
VIRTUAL-RUNTIME_lttng_imx6qsabrelite ?= "packagegroup-tools-lttng2"

PACKAGE_GROUP_tools-benchmark ?= 'packagegroup-tools-benchmark'
PACKAGE_GROUP_tools-lttng     ?= '${VIRTUAL-RUNTIME_lttng}'
PACKAGE_GROUP_codebench-debug ?= 'gdbserver strace openssh-sftp-server'
PACKAGE_GROUP_nas             ?= 'packagegroup-role-nas'
PACKAGE_GROUP_print-server    ?= 'packagegroup-role-print-server'
PACKAGE_GROUP_router          ?= "packagegroup-role-router"

# Analogous to the nfs-server group
PACKAGE_GROUP_samba-server    ?= "samba swat"

# Ensure that the license manifest is complete. Currently, the manifest code
# skips emission of licenses which aren't in deploy/licenses, and does so
# nearly silently. A warning is printed, but it's only visible in the
# do_rootfs task log. This ensures the populate_lic task is run for the deps
# of an image, until the bug is fixed.
do_rootfs[recrdeptask] += "do_populate_lic"

# Bugfix: ensure that changes to INCOMPATIBLE_LICENSE change the checksum of
# affected do_package tasks
do_package[vardeps] += "${@' '.join('LICENSE_EXCLUSION-%s' % p for p in PACKAGES.split())}"

# Sanely handle the GPLv3 gdbserver coming from external-sourcery-toolchain
require conf/distro/include/gdbserver-gplv3.inc
