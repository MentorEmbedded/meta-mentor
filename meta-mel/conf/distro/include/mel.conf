DISTRO_VERSION = "2014.05+snapshot-${DATE}"
MAINTAINER = "Mentor Graphics Corporation <embedded_support@mentor.com>"
TARGET_VENDOR = "-mel"
SDK_VENDOR = "-melsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

# Override poky defaults based on our needs. Removed core-boot and ptest.
POKY_DEFAULT_DISTRO_FEATURES = "largefile opengl multiarch wayland"
POKY_DEFAULT_EXTRA_RDEPENDS = ""
POKY_DEFAULT_EXTRA_RRECOMMENDS = ""

# Paths
MELDIR ?= "${COREBASE}/.."

# Ensure the path to the mel install isn't hardcoded into any signatures
BB_HASHBASE_WHITELIST_append = " MELDIR"

# Application Development Environment
ADE_PROVIDER = "Mentor Graphics Corporation"
ADE_SITENAME = "Application Development Environment for ${DISTRO_NAME}"
ADE_SECTIONS = "devel bootloaders kernel source sdk"
ADE_SECTIONS_EXCLUDED = "locale"

require conf/distro/include/mel-providers.conf
require conf/distro/include/mel-vardeps.conf

# Currently, we do not support CentOS 6 due to its lack of the needed python
# 2.7. We also do not support Debian, SUSE, or openSUSE at this time.
SANITY_TESTED_DISTROS = "\
    Ubuntu-12.04 \n\
    Ubuntu-12.10 \n\
    Ubuntu-13.04 \n\
    Fedora-18 \n\
    Fedora-19 \n\
"

# Forcibly silence PRINC warnings by killing PRINC
PRINC_mel = "0"

# Ensure we can fetch from private github repositories with https
FETCHCMD_wget += "--auth-no-challenge"

INHERIT_DISTRO ?= "debian devshell sstate license deploy-license-manifest"

# Ensure / is mounted r/o in systemd images
INHERIT += "${@' read_only_rootfs_systemd' if 'ro-rootfs' in BBFILE_COLLECTIONS.split() else ''}"

# Add an explicit -march to BUILD_CFLAGS. sanity.bbclass will fail on gcc
# versions older than 4.5 without -march= in BUILD_CFLAGS.
INHERIT += "add_build_arch"

# Support USER_FEATURES
INHERIT += "user_features"

# Use an isolated shared state directory. The user specified SSTATE_DIR will
# still be used, and updated, but the actual SSTATE_DIR used by bitbake will
# be under TMPDIR. This ensures that we can identify what shared state
# archives were used by this build, and means mel-release will only archive
# those shared state archives, not the entire shared SSTATE_DIR, if one
# exists.
INHERIT += "isolated-sstate-dir"

# Do an up front type check to sanity check user configuration
INHERIT += "typecheck"

# Restore any available saved headrevs
DUMP_HEADREVS_DB ?= '${MELDIR}/${MACHINE}/saved_persist_data.db'
INHERIT += "restore-dumped-headrevs"

# Warn if there are available update layers which are not included in the
# current configuration
INHERIT += "mentor-updates-check"

# Ensure we get both 32 and 64 bit pseudo on 64 bit hosts
NO32LIBS ?= "0"

# Make our libdir obey BASE_LIB from the tuning files by default
MULTILIBS ?= ""
require conf/multilib.conf

# locales shouldn't be in lib32/lib64
localedir = "${exec_prefix}/lib/locale"

# SDKs generally only add ${bindir} to the path
sbindir_class-nativesdk = "${bindir}"

# Ensure that we implement shared state reuse handling for non-target recipes
require conf/distro/include/sstate.inc

# Ensure the emitted locale packages are in that section, so they can be
# easily excluded from the ADE (or categorized correctly, when included)
LOCALE_SECTION = "locale"

# A problem we have is that while the info page for ld says that sysv is the
# default in RHEL5, it is actually not and it defaults to gnu hash.  This
# in turn results in binaries that won't run on older systems.  We change to
# forcing both hash types to be included to fix this issue.
BUILD_LDFLAGS += "-Wl,--hash-style=both"

# Additional filesystem types
IMAGE_CLASSES ?= "image_types_uboot image_types_mentor"

# Default to these image types
IMAGE_FSTYPES ?= "tar.bz2 ext3"
UBI_VOLNAME = "rootfs"

# Quadruple the normal. 'du' is not a good way to really see how much
# space will be needed and fails badly as the fs size grows.
IMAGE_ROOTFS_EXTRA_SPACE = "40960"

# Add systemd
VIRTUAL-RUNTIME_init_manager ?= "systemd"
VIRTUAL-RUNTIME_initscripts ?= ""
DISTRO_FEATURES_BACKFILL_CONSIDERED ?= "sysvinit"
DISTRO_FEATURES_append = " systemd"

# Avoid pulling in alsa-state for systemd
VIRTUAL-RUNTIME_alsa-state = "${@'alsa-state' if 'sysvinit' in DISTRO_FEATURES.split() else 'alsa-states'}"

# Add nls if we're supporting.
DISTRO_FEATURES_append = " ${@['','nls'][bb.data.getVar('USE_NLS', d, 1) == 'yes']}"
DISTRO_FEATURES_append = " vfat"

# Ensure fbset is in busybox configuration, and fbset-modes is included
PACKAGECONFIG_append_pn-busybox = " fbset"

# Sane default locales for images
GLIBC_GENERATE_LOCALES ?= "en_US"
IMAGE_LINGUAS ?= "en-us"

# Targets for qemu
QEMU_TARGETS += "mips64 mips64el sh4"

# We prefer busybox rather than tinylogin
VIRTUAL-RUNTIME_login_manager = "busybox"

# Use a local PR server by default
PRSERV_HOST ?= "localhost:0"

# Prefer the chkconfig C implementation of alternatives
VIRTUAL-RUNTIME_update-alternatives = "chkconfig-alternatives"
PREFERRED_PROVIDER_virtual/update-alternatives = "chkconfig-alternatives"
PACKAGECONFIG_remove_pn-opkg-utils = "update-alternatives"
PREFERRED_PROVIDER_virtual/update-alternatives-native = "chkconfig-alternatives-native"
PACKAGECONFIG_remove_pn-opkg-utils-native = "update-alternatives"
PREFERRED_PROVIDER_virtual/nativesdk-update-alternatives = "nativesdk-chkconfig-alternatives"
PACKAGECONFIG_remove_pn-nativesdk-opkg-utils = "update-alternatives"

# We want information about image contents
INHERIT += "buildhistory"
BUILDHISTORY_DIR ?= "${TOPDIR}/buildhistory"
BUILDHISTORY_COMMIT ?= "1"

# Ensure our external toolchain is sane
require conf/distro/include/toolchain-sanity.inc

# Add symlink to ${KERNEL_IMGTYPE}
require conf/distro/include/kernel-link.inc

# Default to ipk packaging
PACKAGE_CLASSES ?= "package_ipk"

# More sane usability for the archiver classes
ARCHIVER_MODE ?= "none"
ARCHIVER_MODE[type] = "choice"
ARCHIVER_MODE[choices] = "none original patched configured"
ARCHIVER_CLASS = "${@'archive-${ARCHIVER_MODE}-source' \
                     if ARCHIVER_MODE != 'none' else ''}"
INHERIT += "${ARCHIVER_CLASS}"

# Ensure we have license-filtered sources available
COPYLEFT_LICENSE_INCLUDE = '*'
COPYLEFT_LICENSE_EXCLUDE = 'CLOSED Proprietary* Mentor Freescale EULA INTEL NetLogic'
COPYLEFT_RECIPE_TYPES ?= '${COPYLEFT_AVAILABLE_RECIPE_TYPES}'
INHERIT += "copyleft_compliance"

# Add do_prepare_copyleft_sources_all task
INHERIT += "copyleft_compliance_all"

# Break up copyleft for use by archive-release
INHERIT += "extra_layerinfo"
COPYLEFT_SOURCES_BASE_DIR ?= '${DEPLOY_DIR}/release-downloads'
RECIPE_LAYER_BASENAME = "${@os.path.basename(get_layer_rootdir(RECIPE_LAYERPATH, d))}"
COPYLEFT_SOURCES_DIR = '${COPYLEFT_SOURCES_BASE_DIR}/${RECIPE_LAYER_BASENAME}'

# Ensure we have what we need for the below type checks
OE_IMPORTS += "oe.terminal"

# Default to no automatic spawned terminals -- expicit is better than implicit
PATCHRESOLVE = "noop"

# Defaults for meta-ti machines missing it, as some recipes require it
MACHINE_KERNEL_PR_beagleboard ?= "r1"

# We aren't quite so picky as poky
WARN_QA = "ldflags useless-rpaths rpaths staticdev libdir xorg-driver-abi \
           textrel already-stripped incompatible-license files-invalid \
           installed-vs-shipped compile-host-path install-host-path \
           pn-overrides \
           "
ERROR_QA = "dev-so debug-deps dev-deps debug-files arch pkgconfig la \
            perms dep-cmp pkgvarcheck perm-config perm-line perm-link \
            split-strip packages-list pkgv-undefined var-undefined \
            "

# Disable reliance upon upstream URIs, as we want our customers to be able to
# build without network connectivity
CONNECTIVITY_CHECK_URIS = ""

# Work around do_populate_lic(sstate_create_package) / do_rootfs race
# Without this, bitbake defaults the dirs to ${B}, which do_rootfs removes due
# to its cleandirs for recipes where ${B} == ${S}.
sstate_create_package[dirs] = "${TOPDIR}"

# Default to the Xorg X server
XSERVER ?= "\
    xserver-xorg \
    xf86-input-evdev \
    xf86-input-mouse \
    xf86-input-keyboard \
    xf86-video-fbdev \
"

# Package groups
FEATURE_PACKAGES_tools-benchmark ?= 'packagegroup-tools-benchmark'
FEATURE_PACKAGES_codebench-debug ?= 'gdbserver strace openssh-sftp-server'
FEATURE_PACKAGES_nas             ?= 'packagegroup-role-nas'
FEATURE_PACKAGES_print-server    ?= 'packagegroup-role-print-server'
FEATURE_PACKAGES_router          ?= "packagegroup-role-router"

# Analogous to the nfs-server group
FEATURE_PACKAGES_samba-server    ?= "samba swat"

require conf/distro/include/mel-versions.conf

# Ensure that the license manifest is complete. Currently, the manifest code
# skips emission of licenses which aren't in deploy/licenses, and does so
# nearly silently. A warning is printed, but it's only visible in the
# do_rootfs task log. This ensures the populate_lic task is run for the deps
# of an image, until the bug is fixed.
do_rootfs[recrdeptask] += "do_populate_lic"

# Bugfix: ensure that changes to INCOMPATIBLE_LICENSE change the checksum of
# affected do_package tasks
do_package[vardeps] += "${@' '.join('LICENSE_EXCLUSION-%s' % p for p in PACKAGES.split())}"

# Sanely handle the GPLv3 gdbserver coming from external-sourcery-toolchain
require conf/distro/include/gdbserver-gplv3.inc

# Allow blacklisting recipes
INHERIT += "blacklist"

# Handle building blocks
require conf/blocks/bluetooth.conf
require conf/blocks/speech-synthesis.conf
require conf/blocks/speech-recognition.conf

# Temperorily mask mesa and qtbase from meta-fsl-arm until
# gpu-viv gets tested and work properly.
BBMASK ?= "(/meta-fsl-arm/(recipes-graphics/mesa/mesa_%|qt5-layer/recipes-qt/qt5/qtbase_%)\.bbappend|/meta-ivi/recipes-multimedia/libtiff/|/meta-ivi/recipes-core-ivi/eglibc/)"

# The user's shell shouldn't be allowed to affect the build (and it can break
# the flock command, if the user's shell isn't POSIX compliant). This should
# go upstream.
SHELL[unexport] = "1"
BB_TERMINAL_EXPORTS += "SHELL"

# Override preferred version of recipe to be used in case of MinnowBoard
PREFERRED_VERSION_xserver-xorg_minnow = '1.14.4'

# Ensure default Xorg X server is used for MinnowBoard
XSERVER_minnow = "xserver-xorg xf86-input-evdev xf86-input-mouse xf86-input-keyboard xf86-video-fbdev"

# Don't include the xorg.conf that makes use of EMGD
BBMASK .= "|/meta-minnow/recipes-graphics/xorg-xserver/xserver-xf86-config_0.1.bbappend"

# Explicitly remove the proprietary stuff
MACHINE_HWCODECS_remove = "va-intel"
XSERVERCODECS_remove = "emgd-driver-video emgd-gst-plugins-va emgd-gst-plugins-mixvideo gst-va-intel"
